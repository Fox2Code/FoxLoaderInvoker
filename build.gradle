plugins {
    id 'application'
    id 'maven-publish'
}

group = 'com.fox2code'
version = '1.1.0'

sourceSets {
    // Test 1: FoxLoader 1.x
    test1.compileClasspath += main.output
    test1.runtimeClasspath += main.output
    // Test 2: FoxLoader 2.x
    test2.compileClasspath += main.output
    test2.runtimeClasspath += main.output
}

configurations {
    test1Implementation.extendsFrom(testImplementation)
    test2Implementation.extendsFrom(testImplementation)
}

repositories {
    mavenCentral()
    maven {
        name = 'Fox2Code'
        url = 'https://cdn.fox2code.com/maven'
    }
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup('maven.modrinth')
        }
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
        content {
            includeGroupAndSubgroups('com.github')
        }
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    test1Implementation 'com.fox2code.FoxLoader:common:1.2.49'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withSourcesJar()
    withJavadocJar()
}

jar {
    from 'LICENSE'
}

application {
    mainClass = 'com.fox2code.foxloader.invoker.Main'
}

afterEvaluate {
    tasks.withType(JavaCompile.class).configureEach {
        options.compilerArgs += '-g'
        options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                groupId = 'com.github.Fox2Code'
                artifactId = project.name
                version = '1.0' // JitPack only work with "1.0" as version
                pom {
                    url = 'https://github.com/Fox2Code/FoxLoaderInvoker'
                }
            }
        }
    }
}
