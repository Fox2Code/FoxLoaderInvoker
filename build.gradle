plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.fox2code'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withSourcesJar()
    withJavadocJar()
}

jar {
    from "LICENSE"

    exclude("com/fox2code/foxloader/loader/**")
    exclude("com/fox2code/foxloader/patching/**")

    manifest {
        attributes 'Main-Class': 'com.fox2code.foxloader.invoker.Main'
    }
}


afterEvaluate {
    tasks.withType(JavaCompile.class).configureEach {
        options.compilerArgs += '-g'
        options.encoding = 'UTF-8'
    }

    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                groupId = "com.github.Fox2Code"
                artifactId = project.name
                version = '1.0' // JitPack only work with "1.0" as version
                pom {
                    url = 'https://github.com/Fox2Code/FoxLoaderInvoker'
                }
            }
        }
    }
}